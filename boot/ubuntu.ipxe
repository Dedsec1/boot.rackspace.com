#!ipxe

# Ubuntu Operating System
# http://www.ubuntu.org

goto ${menu}

:ubuntu
set mirror mirror.rackspace.com
set os ubuntu
set netcfg netcfg/get_ipaddress=${netX/ip} netcfg/get_netmask=${netX/netmask} netcfg/get_gateway=${netX/gateway} netcfg/get_nameservers=${dns} netcfg/confirm_static=true netcfg/disable_autoconfig=true

menu Ubuntu ${arch_a}
item raring Ubuntu 13.04 Raring Ringtail
item quantal Ubuntu 12.10 Quantal Quetzal
item precise Ubuntu 12.04 LTS Precise Pangolin
item oneiric Ubuntu 11.10 Oneiric Ocelot
item natty Ubuntu 11.04 Natty Narwhal
choose version || goto ubuntu_exit
set mirrorcfg mirror/suite=${version} mirror/country=manual mirror/http/hostname=${mirror} mirror/http/directory=/${os}

:deb_boot_type
set dir ${os}/dists/${version}/main/installer-${arch_a}/current/images/netboot/

menu ${os} boot parameters
item install ${os} install
item rescue ${os} rescue mode
item automated ${os} automated install
item expert ${os} expert install
choose --default ${type} type || goto ubuntu

echo ${cls}
goto deb_${type}

:deb_rescue
set install_params rescue/enable=true
goto deb_boot

:deb_automated
set install_params auto=true priority=critical
goto deb_boot

:dev_expert
set install_params priority=low
goto deb_boot

:deb_install
:deb_boot
set dir ${dir}${menu}-installer/${arch_a}
imgfree
echo Boot parameters: ${install_params} ${desktop_param} -- quiet ${params}
kernel http://${mirror}/${dir}/linux ${install_params} ${desktop_param} ${netcfg} ${mirrorcfg} -- quiet ${params}
initrd http://${mirror}/${dir}/initrd.gz
boot

:ubuntu_exit
clear menu
chain linux.ipxe
