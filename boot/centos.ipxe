#!ipxe

# CentOS Operating System
# http://www.centos.org

goto ${menu} ||

:centos
clear version
set os CentOS
iseq ${manufacturer} Xen && set netcfg ip=${netX/ip} netmask=${netX/netmask} gateway=${netX/gateway} dns=${dns} ksdevice=eth0 ||
menu ${os} ${arch}
item 6.5 ${os} 6.5
item 6.4 ${os} 6.4
item 6.3 ${os} 6.3
item other Choose other version [o]
isset ${version} || choose version || goto linux_menu
iseq ${version} other || goto centos_skip_read_version
echo ${cls}
echo -n Enter version: ${} && read version
:centos_skip_read_version
set dir ${menu}/${version}/os/${arch}
set repo repo=${mirror}/${dir}
goto boottype

:boottype
set ova ${os} ${version}
menu ${os} ${arch} boot type
item normal ${ova}
item rescue ${ova} rescue
item kickstart ${ova} specify kickstart url
isset ${bt} || choose bt || goto centos
echo ${cls}
iseq ${bt} rescue && goto rescue ||
iseq ${bt} kickstart && goto kickstart ||
goto bootos_images

:rescue
set params rescue ||
goto bootos_images

:kickstart
echo -n Specify kickstart URL for ${os} ${version}: && read ksurl
set params ks=${ksurl} ||
goto bootos_images

:bootos_images
imgfree
kernel ${mirror}/${dir}/isolinux/vmlinuz ${repo} ${params} ${netcfg} clocksource=hpet
initrd ${mirror}/${dir}/isolinux/initrd.img
boot
goto linux_menu

:linux_menu
clear menu
chain linux.ipxe
exit 0
