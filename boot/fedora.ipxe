#!ipxe

# Fedora Operating System
# http://wwww.fedora.org

goto ${menu} ||

:fedora
clear osversion
set os Fedora
menu Fedora ${arch}
iseq ${manufacturer} Xen && set netcfg ip=${netX/ip} netmask=${netX/netmask} gateway=${netX/gateway} dns=${dns} ksdevice=eth0 ||
item 20 ${os} 20
item 19 ${os} 19
item 18 ${os} 18
isset ${osversion} || choose osversion || goto linux_menu
set dir fedora/releases/${osversion}/${os}/${arch}/os
set repo http://${mirror}/${dir}
goto boottype

:boottype
set ova ${os} ${osversion}
menu ${os} ${arch} boot type
item normal ${ova}
item rescue ${ova} rescue
item kickstart ${ova} specify kickstart url
isset ${bt} || choose bt || goto fedora
echo ${cls}
iseq ${bt} rescue && goto rescue ||
iseq ${bt} kickstart && goto kickstart ||
goto bootos_images

:rescue
set params rescue ||
goto bootos_images

:kickstart
echo -n Specify kickstart URL for ${os} ${osversion}: && read ksurl
set params ks=${ksurl} ||
goto bootos_images

:bootos_images
imgfree
kernel http://${mirror}/${dir}/isolinux/vmlinuz repo=${repo} ${params} ${netcfg} initrd=initrd.img
initrd http://${mirror}/${dir}/isolinux/initrd.img
boot
goto linux_menu

:linux_menu
clear menu
chain linux.ipxe
exit 0
