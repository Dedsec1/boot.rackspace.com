#!ipxe

# Fedora Operating System
# http://wwww.fedora.org

goto ${menu} ||

:fedora
clear version
set os Fedora
menu Fedora ${arch}
iseq ${manufacturer} Xen && set netcfg ip=${netX/ip} netmask=${netX/netmask} gateway=${netX/gateway} dns=${dns} ksdevice=eth0 ||
item 20 ${os} 20
item 19 ${os} 19
item 18 ${os} 18
isset ${version} || choose version || goto linux_menu
set dir fedora/releases/${version}/${os}/${arch}/os
set repo repo=${mirror}/${dir}
goto boottype

:boottype
set ova ${os} ${version}
menu ${os} ${arch} boot type
item normal ${ova}
item rescue ${ova} rescue
isset ${bt} || choose bt || goto fedora
echo ${cls}
iseq ${bt} rescue && set params rescue ||
goto bootos_images

:bootos_images
imgfree
kernel ${mirror}/${dir}/isolinux/vmlinuz ${repo} ${params} ${netcfg}
initrd ${mirror}/${dir}/isolinux/initrd.img
boot
goto linux_menu

:linux_menu
clear menu
chain linux.ipxe
exit 0
