#!ipxe

goto ${menu} ||

:bsd_menu

menu BSD Operating Systems - Current Selected Architecture [ ${arch} ]
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}

# BSD based systems
item --gap BSD Based Operating Systems
item openbsd ${space} OpenBSD
item freebsd ${space} FreeBSD

# Options
item --gap Options:
iseq ${arch} x86_64 && set bits 64 || set bits 32
item changebits ${space} Architecture: ${arch} (${bits}bit)
choose menu || goto bsd_exit

echo ${cls}
goto ${menu} ||
chain ${menu}.ipxe || goto error
goto bsd_exit

:bsd_exit
clear menu
chain menu.ipxe

:changebits
iseq ${arch} x86_64 && set arch i386 || set arch x86_64
goto bsd_menu

:openbsd
chain openbsd.ipxe
goto bsd_menu

:freebsd
chain freebsd.ipxe
goto bsd_menu
